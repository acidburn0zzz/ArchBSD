# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxres
pkgver=1.0.6
pkgrel=1
pkgdesc="X11 Resource extension library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org"
license=('custom')
depends=('libxext')
makedepends=('resourceproto' 'damageproto' 'compositeproto' 'scrnsaverproto' 'xorg-util-macros')
source=("${url}/releases/individual/lib/libXres-${pkgver}.tar.bz2")
sha1sums=('31a9b7d4f7a978de36c6f1c867dced29bfe7ef0f')

build() {
  cd "${srcdir}/libXres-${pkgver}"
  ./configure --prefix=/usr/local --sysconfdir=/usr/local/etc
  make
}

package() {
  cd "${srcdir}/libXres-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/local/libdata"
  mv "${pkgdir}/usr/local/lib/pkgconfig" \
     "${pkgdir}/usr/local/libdata/pkgconfig"

  install -m755 -d "${pkgdir}/usr/local/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/local/share/licenses/${pkgname}/"
}
