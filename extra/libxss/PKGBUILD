# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=libxss
pkgver=1.2.2
pkgrel=1
pkgdesc="X11 Screen Saver extension library"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxext' 'scrnsaverproto')
makedepends=('xorg-util-macros')
source=("${url}/releases/individual/lib/libXScrnSaver-${pkgver}.tar.bz2")
sha1sums=('7b8298eec371c33a71232e3653370a98f03c6c88')

build() {
  cd "${srcdir}/libXScrnSaver-${pkgver}"
  ./configure --prefix=/usr/local --sysconfdir=/usr/local/etc
  make
}

package() {
  cd "${srcdir}/libXScrnSaver-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/local/libdata"
  mv "${pkgdir}/usr/local/lib/pkgconfig" \
     "${pkgdir}/usr/local/libdata/pkgconfig"

  install -m755 -d "${pkgdir}/usr/local/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/local/share/licenses/${pkgname}/"
}
