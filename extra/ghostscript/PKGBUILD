# $Id: PKGBUILD 165016 2012-08-08 19:40:49Z andyrtr $
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=ghostscript
pkgver=9.06
pkgrel=1
pkgdesc="An interpreter for the PostScript language"
arch=('i686' 'x86_64')
license=('GPL3' 'custom')
#depends=('libxt' 'libcups' 'fontconfig' 'jasper' 'libpng>=1.5.7' 'libjpeg' 'libtiff>=4.0.0' 'lcms') # 'lcms2' won't get used) # move in libpaper from community?
depends=('libxt' 'libcups' 'fontconfig' 'libpng>=1.5.7' 'libjpeg' 'libtiff>=4.0.0')
makedepends=('gtk2') # 'gnutls')
optdepends=('texlive-core:      needed for dvipdf'
            'gtk2:              needed for gsx')
url="http://www.ghostscript.com/"
options=('!makeflags')
source=(http://downloads.ghostscript.com/public/ghostscript-${pkgver}.tar.bz2)
md5sums=('46f9ebe40dc52755287b30704270db11')

build() {
  cd ${srcdir}/ghostscript-${pkgver}

  # force it to use system-libs
  # rm -rf jpeg libpng zlib jasper expat tiff lcms freetype 
  rm -rf jpeg libpng zlib expat tiff freetype lcms lcms2 jasper openjpeg

  export CFLAGS="${CFLAGS} -I/usr/local/include"
  export CPPFLAGS="${CPPFLAGS} -I/usr/local/include"

	#--enable-contrib 
  ./configure --prefix=/usr/local \
	--enable-dynamic \
	--with-ijs \
	--with-jbig2dec \
	--with-omni \
	--with-x \
	--with-drivers=ALL \
	--with-install-cups \
	--enable-fontconfig \
	--enable-freetype \
	--enable-dynamic \
	--without-luratech \
	--with-system-libtiff \
	--without-jasper \
	--disable-compile-inits #--help # needed for linking with system-zlib
  export CFLAGS_STANDARD="${CFLAGS}"
  export SOC_LOADER="dxmainc.c"
  gmake

  # Build IJS
  cd ${srcdir}/ghostscript-${pkgver}/ijs
  ./autogen.sh
  ./configure --prefix=/usr/local --enable-shared --disable-static
  gmake
}

package() {
  cd ${srcdir}/ghostscript-${pkgver}
  gmake DESTDIR=${pkgdir} install soinstall
#   cups_serverroot=${pkgdir}/usr/local/etc/cups \
#   cups_serverbin=${pkgdir}/usr/local/lib/cups install soinstall

  # install missing doc files # http://bugs.archlinux.org/task/18023
  install -dm755 ${pkgdir}/usr/local/share/ghostscript/$pkgver/doc/
  for i in ${srcdir}/ghostscript-${pkgver}/doc/{Ps2ps2.htm,gs-vms.hlp,gsdoc.el,pscet_status.txt}
  do
    install -m644 "$i" ${pkgdir}/usr/local/share/ghostscript/$pkgver/doc/
  done
  
  mkdir -p ${pkgdir}/usr/local/share/licenses/${pkgname}
  install -m644 LICENSE ${pkgdir}/usr/local/share/licenses/${pkgname}/

  # remove unwanted localized man-pages
  rm -rf $pkgdir/usr/local/share/man/[^man1]*

  # install IJS
  cd ${srcdir}/ghostscript-${pkgver}/ijs
  gmake DESTDIR=${pkgdir} install
}
