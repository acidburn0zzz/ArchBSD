# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=gdk-pixbuf2
pkgver=2.23.5
pkgrel=1
pkgdesc="An image loading library"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL2.1')
depends=('glib2' 'libpng' 'libtiff' 'libjpeg' 'libx11')
makedepends=('gobject-introspection')
#options=('!libtool')
install=gdk-pixbuf2.install
source=(http://download.gnome.org/sources/gdk-pixbuf/${pkgver%.*}/gdk-pixbuf-$pkgver.tar.xz
        patch-gdk-pixbuf_io-xbm.c)
sha256sums=('7a6c3550426f38f6b0eeeb49295654d9e706ce8f1786a4d0932f9d2c0d0bc500'
            '5c16e7f6f11b275f7ed5e9a5a4dff351e1cf1586a18dda066f0886b7a4a71c39')

build() {
  cd gdk-pixbuf-$pkgver

# NOTE: THIS patch is already added to 2.26.* - when updating (which requires a glib2 update) remove this
  patch -p0 -i "$srcdir"/patch-gdk-pixbuf_io-xbm.c

  ./configure --prefix=/usr/local \
    --without-libjasper \
    --with-x11 \
    --with-included-loaders=png
# ltverhack:
  for i in ltmain.sh libtool; do
    sed -i '' -e '/freebsd-elf)/,/;;/ s|major="\.$current"|major=.0|' \
              -e '/freebsd-elf)/,/;;/ s|versuffix="\.$current"|versuffix="$major"|' \
        $i
  done
  gmake
}

package() {
  cd gdk-pixbuf-$pkgver

  gmake DESTDIR="$pkgdir" install

  install -dm755 "${pkgdir}/usr/local/libdata"
  mv "${pkgdir}/usr/local/lib/pkgconfig" \
     "${pkgdir}/usr/local/libdata/pkgconfig"
}

# vim:set ts=2 sw=2 et:
