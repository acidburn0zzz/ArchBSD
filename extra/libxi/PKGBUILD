# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxi
pkgver=1.6.2
pkgrel=1
pkgdesc="X11 Input extension library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org"
depends=('libxext' 'inputproto')
makedepends=('pkgconf' 'xorg-util-macros')
license=('custom')
source=(${url}/releases/individual/lib/libXi-${pkgver}.tar.bz2)
sha256sums=('3250fd34cdd099a15b78abdb87c7e1a9b7a2188ab893f273e90d831dbc604058')

build() {
  cd "${srcdir}/libXi-${pkgver}"
  ./configure --prefix=/usr/local --sysconfdir=/usr/local/etc \
              --disable-specs --without-xmlto
  make
}

package() {
  cd "${srcdir}/libXi-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/local/libdata"
  mv "${pkgdir}/usr/local/lib/pkgconfig" \
     "${pkgdir}/usr/local/libdata/pkgconfig"

  install -m755 -d "${pkgdir}/usr/local/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/local/share/licenses/${pkgname}/"
}
