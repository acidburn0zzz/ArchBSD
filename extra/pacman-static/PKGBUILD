_pkgname=pacman
pkgname=pacman-static
pkgver=4.0.3
pkgrel=7
pkgdesc="A library-based package manager with dependency support"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('bash' 'pacman-mirrorlist' 'archbsd-keyring')
makedepends=('asciidoc' 'gsed' 'gmake')
optdepends=('fakeroot: for makepkg usage as normal user')
source=(ftp://ftp.archlinux.org/other/pacman/$_pkgname-$pkgver.tar.gz
        0001-Add-conflict-for-replacing-owned-empty-directory.patch
        0002-Check-empty-subdirectory-ownership.patch
        0003-Ensure-pre_upgrade-scriptlet-gets-old-package-version.patch
        patch-pacman-static-Makefile.in
        )
md5sums=('387965c7125e60e5f0b9ff3b427fe0f9'
         'e0c7c9026dfbb5a042a6cb05c3810fd6'
         '02db451be806335ce189ffadb5cf84b9'
         '9926eb703225b8e34d54e222685d63d8'
         'f5960c80226b2098d9d199c5910c12b8')

build() {
  cd $srcdir/$_pkgname-$pkgver

  patch -p1 -i $srcdir/0001-Add-conflict-for-replacing-owned-empty-directory.patch
  patch -p1 -i $srcdir/0002-Check-empty-subdirectory-ownership.patch
  patch -p1 -i $srcdir/0003-Ensure-pre_upgrade-scriptlet-gets-old-package-version.patch
 
  patch -p1 -i $srcdir/patch-pacman-static-Makefile.in

  ./configure --prefix=/usr/local --sysconfdir=/usr/local/etc \
    --localstatedir=/var --disable-doc LDFLAGS="-L/usr/local/lib" \
   CPPFLAGS="-I/usr/local/include -I/usr/local/include/curl -I/usr/local/include/gpgme" \
   --disable-shared --enable-static

  cd src/pacman
  gmake LDFLAGS="-L/usr/local/lib -all-static" LIBS="-lalpm -lgpgme"
}

package() {
  cd $srcdir/$_pkgname-$pkgver/src/pacman

  install -dm755 "${pkgdir}/usr/local/bin"
  install -m755 pacman "${pkgdir}/usr/local/bin/pacman-static"
}
