#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-fonts-encodings
pkgver=1.0.4
pkgrel=4
pkgdesc="X.org font encoding files"
arch=(any)
url="http://xorg.freedesktop.org/"
license=('custom')
makedepends=('xorg-mkfontscale' 'xorg-util-macros' 'xorg-font-util')
groups=('xorg-fonts' 'xorg')
options=(!makeflags)
source=(${url}/releases/individual/font/encodings-${pkgver}.tar.bz2)
sha1sums=('24ace2470403f85a0d393769204029bd8247992a')

build() {
  cd "${srcdir}/encodings-${pkgver}"
  export FONTROOTDIR=/usr/local/share/fonts
  ./configure --prefix=/usr/local --with-fontrootdir=$FONTROOTDIR
  make
}

package() {
  cd "${srcdir}/encodings-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/local/share/licenses/${pkgname}/"
  install -m644 COPYING "${pkgdir}/usr/local/share/licenses/${pkgname}/COPYING"

  # regenerate encodngs file not to include non-compressed versions
  cd $pkgdir/usr/local/share/fonts/encodings/large
  mkfontscale -b -s -l -n -r -p /usr/local/share/fonts/encodings/large -e . .
  cd $pkgdir/usr/local/share/fonts/encodings/
  mkfontscale -b -s -l -n -r -p /usr/local/share/fonts/encodings -e . -e large .
}
