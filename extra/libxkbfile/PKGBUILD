# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxkbfile
pkgver=1.0.8
pkgrel=1
pkgdesc="X11 keyboard file manipulation library"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libx11')
makedepends=('xorg-util-macros')
source=("${url}/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('05139829837c26e3a14a40a700f058c018bc71f5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr/local --sysconfdir=/usr/local/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/local/libdata"
  mv "${pkgdir}/usr/local/lib/pkgconfig" \
     "${pkgdir}/usr/local/libdata/pkgconfig"

  install -m755 -d "${pkgdir}/usr/local/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/local/share/licenses/${pkgname}/"
}
