# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libdca
pkgver=0.0.5
pkgrel=3
pkgdesc="Free library for decoding DTS Coherent Acoustics streams"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.videolan.org/developers/libdca.html"
provides=('libdts')
replaces=('libdts')
options=('!emptydirs')
source=("http://download.videolan.org/pub/videolan/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('dab6b2795c66a82a6fcd4f8343343021')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i '' \
      -e 's@pkgconfigdir = \$(libdir)/pkgconfig@pkgconfigdir = $(prefix)/libdata/pkgconfig@' \
      libdca/Makefile.in
  ./configure
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}/usr/local/man/man1"/{extract_dts,dtsdec}.1
  ln -s dcadec.1.gz "${pkgdir}/usr/local/man/man1/dtsdec.1.gz"
  ln -s extract_dca.1.gz "${pkgdir}/usr/local/man/man1/extract_dts.1.gz"
  # remove dangling symlink
  rm "${pkgdir}/usr/local/lib/libdts.a"
}
