# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=libxrandr
pkgver=1.4.0
pkgrel=1
pkgdesc="X11 RandR extension library"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxext' 'libxrender' 'randrproto>=1.4.0')
makedepends=('xorg-util-macros')
source=(${url}/releases/individual/lib/libXrandr-${pkgver}.tar.bz2)
sha256sums=('033ad0ac2f012afb05268660f6d78705c85f84689f92fa7b47ce12959b15f5c3')

build() {
  cd "${srcdir}/libXrandr-${pkgver}"
  ./configure --prefix=/usr/local --sysconfdir=/usr/local/etc
  make
}

package() {
  cd "${srcdir}/libXrandr-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/local/libdata"
  mv "${pkgdir}/usr/local/lib/pkgconfig" \
     "${pkgdir}/usr/local/libdata/pkgconfig"

  install -d -m755 "${pkgdir}/usr/local/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/local/share/licenses/${pkgname}/"
}
