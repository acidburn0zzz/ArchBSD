# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=cups-filters
pkgver=1.0.29
pkgrel=3
pkgdesc="OpenPrinting CUPS Filters"
arch=('i686' 'x86_64')
url="http://www.linuxfoundation.org/collaborate/workgroups/openprinting"
license=('GPL')
depends=('lcms2' 'poppler>=0.22.0' 'qpdf>=4.0.0')
makedepends=('ghostscript' 'ttf-dejavu' 'libcups') # ttf-dejavu for make check
optdepends=('ghostscript: for non-PostScript printers to print with CUPS to convert PostScript to raster images'
	    'foomatic-db: drivers use Ghostscript to convert PostScript to a printable form directly'
	    'foomatic-db-engine: drivers use Ghostscript to convert PostScript to a printable form directly'
	    'foomatic-db-nonfree: drivers use Ghostscript to convert PostScript to a printable form directly')
backup=(etc/fonts/conf.d/99pdftoopvp.conf)
options=(!libtool)
source=(http://www.openprinting.org/download/cups-filters/$pkgname-$pkgver.tar.gz
        poppler_buildfix.diff)
md5sums=('0640a02a3fb88d3dbdb224e3becd400d'
         'bd97d1d9dbd4feec3a0330fd9fb7a03c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  # fix build with poppler 0.22
  patch -Np0 -i ${srcdir}/poppler_buildfix.diff

#  sed -i '' -e 's/AM_CONFIG_HEADER/AC_CONFIG_HEADER/g' configure.ac
#  sed -i '' -e 's/_PROGRAMS = pdftoopvp/_PROGRAMS = /' Makefile.am
#  autoreconf --install

  ZLIB_CFLAGS=" " \
  ZLIB_LIBS="-lz" \
  LIBQPDF_CFLAGS="`pkg-config --cflags libqpdf`" \
  LIBQPDF_LIBS="`pkg-config --libs libqpdf`" \
  PKG_CONFIG_PATH="${PKG_CONFIG_PATH}:/usr/local/libdata/pkgconfig" \
  CFLAGS="$CFLAGS" \
  CXXFLAGS="$CXXFLAGS -stdlib=libc++ -std=c++11" \
  CPPFLAGS="$CPPFLAGS -I/usr/local/include" \
  LDFLAGS="$LDFLAGS -L/usr/local/lib" \
  ./configure --prefix=/usr/local \
    --sysconfdir=/usr/local/etc \
    --with-rcdir=no \
    --with-test-font-path=/usr/local/lib/X11/fonts/dejavu/DejaVuSans.ttf
  sed -i '' -e 's/^\(DLOPEN_LIBS =\).*$/\1/' Makefile
  gmake
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -j1 -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
