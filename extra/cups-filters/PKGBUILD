# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=cups-filters
pkgver=1.0.29
pkgrel=3
pkgdesc="OpenPrinting CUPS Filters"
arch=('i686' 'x86_64')
url="http://www.linuxfoundation.org/collaborate/workgroups/openprinting"
license=('GPL')
depends=('lcms2' 'poppler>=0.22.0' 'qpdf>=4.0.0')
makedepends=('ghostscript' 'ttf-dejavu') # ttf-dejavu for make check
optdepends=('ghostscript: for non-PostScript printers to print with CUPS to convert PostScript to raster images'
	    'foomatic-db: drivers use Ghostscript to convert PostScript to a printable form directly'
	    'foomatic-db-engine: drivers use Ghostscript to convert PostScript to a printable form directly'
	    'foomatic-db-nonfree: drivers use Ghostscript to convert PostScript to a printable form directly')
backup=(etc/fonts/conf.d/99pdftoopvp.conf)
options=(!libtool)
source=(http://www.openprinting.org/download/cups-filters/$pkgname-$pkgver.tar.gz
        cups-browsed.service
        poppler_buildfix.diff)
md5sums=('0640a02a3fb88d3dbdb224e3becd400d'
         '4c9f97eae201798000483c4e7109e690'
         'bd97d1d9dbd4feec3a0330fd9fb7a03c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  # fix build with poppler 0.22
  patch -Np0 -i ${srcdir}/poppler_buildfix.diff
  
  ./configure --prefix=/usr/local \
    --sysconfdir=/usr/local/etc \
    --with-rcdir=no \
    --with-test-font-path=/usr/local/lib/X11/fonts/dejavu/DejaVuSans.ttf
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -j1 -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
