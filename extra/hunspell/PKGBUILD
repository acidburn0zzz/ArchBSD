# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Hussam Al-Tayeb <ht990332@gmail.com>

pkgname=hunspell
pkgver=1.3.2
pkgrel=2
pkgdesc="Spell checker and morphological analyzer library and program"
arch=('i686' 'x86_64')
url="http://hunspell.sourceforge.net/"
license=('GPL' 'LGPL' 'MPL')
optdepends=('perl: for ispellaff2myspell')
source=(http://downloads.sourceforge.net/hunspell/hunspell-$pkgver.tar.gz
        patch-src_tools_hunspell.cxx)
md5sums=('3121aaf3e13e5d88dfff13fb4a5f1ab8'
         '4a8b14bc954b0cfed1368d44649256f9')

build() {
  cd "$srcdir/hunspell-$pkgver"
  patch -p0 -i "$srcdir"/patch-src_tools_hunspell.cxx
  sed -i '' -e 's|%%PREFIX%%|/usr/local|g' src/tools/hunspell.cxx
  CPPFLAGS="${CPPFLAGS} -I/usr/local/include" \
  ./configure --prefix=/usr/local --with-ui --with-experimental \
              --with-libiconv-prefix=/usr/local \
              --with-readline
  make -j1
}

package() {
  cd "$srcdir/hunspell-$pkgver"
  gmake DESTDIR="$pkgdir" install -j1
  
  # add generic hunspell.so for development and projects not using pkgconfig flags - FS#30592
  pushd $pkgdir/usr/local/lib
  ln -s libhunspell-?.?.so libhunspell.so
  popd

  install -dm755 "${pkgdir}/usr/local/libdata"
  mv "${pkgdir}/usr/local/lib/pkgconfig" \
     "${pkgdir}/usr/local/libdata/pkgconfig"
}
