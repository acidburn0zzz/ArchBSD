# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=libxaw
pkgver=1.0.11
pkgrel=1
pkgdesc="X11 Athena Widget library"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxmu' 'libxpm')
makedepends=('xorg-util-macros' 'xmlto')
source=(${url}/releases/individual/lib/libXaw-${pkgver}.tar.bz2)
sha256sums=('87a874acbb71d1ee59b8047312fc91d89dcb373e2970f121184cdae29a3d9492')

build() {
  cd "${srcdir}/libXaw-${pkgver}"
  ./configure --prefix=/usr/local --sysconfdir=/usr/local/etc
  make
}

package() {
  cd "${srcdir}/libXaw-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/local/libdata"
  mv "${pkgdir}/usr/local/lib/pkgconfig" \
     "${pkgdir}/usr/local/libdata/pkgconfig"

  install -dm755 "${pkgdir}/usr/local/share/licenses/${pkgname}/"
  install -m644 COPYING "${pkgdir}/usr/local/share/licenses/${pkgname}/COPYING"
}
