# $Id: PKGBUILD 157383 2012-04-28 09:25:57Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libffi
pkgver=3.0.9
pkgrel=1
pkgdesc="A portable, high level programming interface to various calling conventions"
arch=('i686' 'x86_64')
url="http://sourceware.org/libffi"
license=('MIT')
# if you don't have this use makepkg --nocheck
checkdepends=('dejagnu')
install=libffi.install
source=(ftp://sourceware.org/pub/libffi/libffi-${pkgver}.tar.gz
        patch-configure
	patch-src_arm_sysv.S)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i "${srcdir}/patch-configure"
  patch -p0 -i "${srcdir}/patch-src_arm_sysv.S"
  ./configure --prefix=/usr/local --mandir=/usr/local/man --infodir=/usr/local/info
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -dm755 $"${pkgdir}/usr/local/libdata"
  mv "${pkgdir}/usr/local/lib/pkgconfig" "${pkgdir}/usr/local/libdata/pkgconfig"

  install -dm755 "$pkgdir"/usr/local/include
  ln -sf ../lib/libffi-${pkgver}/include/ffi.h "$pkgdir"/usr/local/include/
  ln -sf ../lib/libffi-${pkgver}/include/ffitarget.h "$pkgdir"/usr/local/include/

  install -d -m755 "${pkgdir}/usr/local/share/licenses/${pkgname}"
  install    -m644 LICENSE "${pkgdir}/usr/local/share/licenses/${pkgname}/LICENSE"
}
md5sums=('1f300a7a7f975d4046f51c3022fa5ff1'
         '9e3d64d600cd9686faaf8b3a7a3c18c5'
         '39bec3dabfc8bc98e4c3724e1cc08077')
