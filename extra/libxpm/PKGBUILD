# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=libxpm
pkgver=3.5.10
pkgrel=1
pkgdesc="X11 pixmap library"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxt' 'libxext')
makedepends=('xorg-util-macros')
source=("${url}/releases/individual/lib/libXpm-${pkgver}.tar.bz2")
sha1sums=('eca9dbfa6253f7d6cc7d593dc83110c6c4876d7c')

build() {
  cd "${srcdir}/libXpm-${pkgver}"
  ./configure --prefix=/usr/local --sysconfdir=/usr/local/etc
  make
}

package() {
  cd "${srcdir}/libXpm-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/local/libdata"
  mv "${pkgdir}/usr/local/lib/pkgconfig" \
     "${pkgdir}/usr/local/libdata/pkgconfig"

  install -m755 -d "${pkgdir}/usr/local/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/local/share/licenses/${pkgname}/"
}
