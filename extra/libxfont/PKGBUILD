# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxfont
pkgver=1.4.5
pkgrel=1
pkgdesc="X11 font rasterisation library"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('freetype2' 'libfontenc' 'xproto' 'fontsproto')
makedepends=('xorg-util-macros' 'xtrans')
source=(${url}/archive/individual/lib/libXfont-${pkgver}.tar.bz2)
sha1sums=('0bf3cb8aa192ab96bbaf384d1a4a00d15a40171e')

build() {
  cd "${srcdir}/libXfont-${pkgver}"
  ./configure --prefix=/usr/local --sysconfdir=/usr/local/etc \
              --without-xmlto --disable-devel-docs
  make
}

package() {
  cd "${srcdir}/libXfont-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/local/libdata"
  mv "${pkgdir}/usr/local/lib/pkgconfig" \
     "${pkgdir}/usr/local/libdata/pkgconfig"

  install -m755 -d "${pkgdir}/usr/local/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/local/share/licenses/${pkgname}/"
}
