# $Id: PKGBUILD 172097 2012-11-28 10:31:12Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=glib2
pkgver=2.28.8
pkgrel=5
pkgdesc="Common C routines used by GTK+ and other libs"
url="http://www.gtk.org/"
arch=(i686 x86_64)
license=('LGPL')
depends=('pcre' 'libffi' 'python2' 'pkgconf')
makedepends=('pkg-config' 'python2' 'libxslt' 'docbook-xml')
optdepends=('python2: for gdbus-codegen')
options=('!libtool' '!docs' '!emptydirs')
install=glib2.install
source=(http://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver%.*}/glib-$pkgver.tar.xz
        patch-ae
        patch-ag
        patch-ah
        patch-config.h.in
        patch-gio_gdesktopappinfo.c
        patch-gio_gunixcredentialsmessage.c
        patch-gio_gunixmount.c
        patch-gio_gunixmounts.c
        patch-gio_gunixvolume.c
        patch-gio_libasyncns_asyncns.c
        patch-gio_tests_gdbus-peer.c
        patch-gio_xdgmime_xdgmime.c
        patch-gio_xdgmime_xdgmimecache.h
        patch-glib-2.0.pc.in
        patch-glib__libcharset__Makefile.in
        patch-glib_Makefile.in
        patch-glib_fix_hidden
        patch-glib_gregex.c
        patch-glib_gutils.c
        patch-gmodule__gmodule-dl.c
        patch-gobject_Makefile.in
        patch-gthread_gthread-posix.c
        )
sha256sums=('4d7ca95dbde8e8f60ab428c765b0dbb8a44be9eb9316491803ce5ee7b4748353'
            '45eac5e2f0f67c28500883a3877237dcd27e26bbf6e8f41a1dce049a8900afb8'
            'd5311f95187697251a70e4d0ecd4af13fb38a2d6b8a36a8a266364c31ce2318c'
            '718fdd920f7e54337daa7377c58e6e3e9474c15712805bd1d1db1c00252b2e67'
            '9b3e795e34f15d621a822bc46f47b1022f798fff1d577dab60a839918f4e6a26'
            'e4907cd90d673ce9180b91f1727d5a22ad791b57e63eac833339333fe708ae63'
            '9a8327512292208f793035811302949c50363c068c8fd7a348d8ab06ace679da'
            '4aa14581d85f760bc89dae79d3b98df44b4449fbda89f3783798c58199a6eb6d'
            '7b954311655c5afcdb56b71ceda8c37b5b0bc7dc52549403a888b26101162235'
            'a700fe182cd60a226e0baea003302fd7851bc69abb23379fd99821b722712f04'
            'ef74b0a3eaba970b079ffa950a865a6a096cae2bc6c86d867b05daf7f05b1e07'
            '6b207bbd9155b5012ba644d374ee65bb54171c806fee8786ded51694ca3f08e3'
            'b0768aa990688492df0a6c7544d2c9e40cbb7453eb22fcb1376a027397526178'
            'e74504a5b30451215a1259017a907e5e2ec729c33fe06985fb51a5288c6299c1'
            '6cdabab4a264a8ced0b7d6afa3dc9ab7d75d2585ed2efa9e6bd3c70009390cdc'
            '9fe652723a3350c0cf7ed531c55d5a6b43e173ee4d8e6a729ff29033e6f3375b'
            '23d7b0c33b98a736abaa1c519451ab6a653e4ba214a72b9826e2c7000fb4a918'
            '0f75caaf81e6040f0b864ba2053bf884832f2bc1d48cddebd4f7f0ef916e7d36'
            '5d156afd19eadd1152ac776a2c1eda7200d5aca951882cdcfb2f8fe4debf5074'
            'fd7948ddc82123e367a79a4cbdff50cb88cbe5858563cfb96be28e14da26df6b'
            '52f3628550410b355b29ecce0564b86587490c42b53dc222d786436a782bf8d0'
            '0389010ef5b6dd69c4468b0fe232650cd0e1d902bf0349043ab902f0b950ded5'
            '32eccbad7c475d64770644961852f5278e6d49825965e8182b1bbea593686ae5')

build() {
  cd glib-$pkgver

  for i in patch-ae patch-ag patch-ah patch-config.h.in patch-gio_gdesktopappinfo.c patch-gio_gunixcredentialsmessage.c patch-gio_gunixmount.c patch-gio_gunixmounts.c patch-gio_gunixvolume.c patch-gio_libasyncns_asyncns.c patch-gio_tests_gdbus-peer.c patch-gio_xdgmime_xdgmime.c patch-gio_xdgmime_xdgmimecache.h patch-glib-2.0.pc.in patch-glib_Makefile.in patch-glib__libcharset__Makefile.in patch-glib_fix_hidden patch-glib_gregex.c patch-glib_gutils.c patch-gmodule__gmodule-dl.c patch-gobject_Makefile.in patch-gthread_gthread-posix.c
  do
    patch -p0 -i "${srcdir}/$i"
  done

  sed -i '' -e 's|/usr/share/locale/locale|/usr/local/share/locale/locale|g' \
        glib/gutils.c
  sed -i '' -e 's|/lib/dbus/machine-id|/db/dbus/machine-id|g' \
    gio/gdbusaddress.c gio/gdbusprivate.c \
    po/*.po
  sed -i '' -e 's|inotify_support=yes|inotify_support=no| ; s|#define HAVE_SYS_INOTIFY_H 1||' configure

  PYTHON=/usr/local/bin/python2 ./configure --prefix=/usr/local --libdir=/usr/local/lib \
      --sysconfdir=/usr/local/etc \
      --enable-static --enable-shared --with-libiconv=gnu \
      --disable-gtk-doc --with-html-dir=/usr/local/share/doc \
      --disable-man --without-xml-catalog \
      --disable-dtrace \
      --with-pcre=system \
      --disable-fam \
      --target=$CHOST \
      LDFLAGS="${LDFLAGS} -L/usr/local/lib -lintl" \
      PTHREAD_CFLAGS="-pthread" \
      PTHREAD_LIBS="-pthread" \
      CFLAGS="${CFLAGS} -I/usr/local/include" \
      CPPFLAGS="${CPPFLAGS} -I/usr/local/include"
  gmake
}

package() {
  cd glib-$pkgver
  gmake completiondir=/usr/local/etc/bash-completion.d DESTDIR="$pkgdir" install

  for _i in "$pkgdir/usr/local/etc/bash-completion.d/"*; do
      chmod -x "$_i"
  done
# our package doesn't create this
  # sed -i '' -e "s|#!/usr/bin/env python|#!/usr/bin/env python2|" "$pkgdir"/usr/local/bin/gdbus-codegen

  install -dm755 "${pkgdir}/usr/local/libdata"
  mv "${pkgdir}/usr/local/lib/pkgconfig" "${pkgdir}/usr/local/libdata/pkgconfig"
}
