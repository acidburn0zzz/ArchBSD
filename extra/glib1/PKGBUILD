# $Id: PKGBUILD 135061 2011-08-10 01:01:44Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=glib
pkgver=1.2.10
pkgrel=9
pkgdesc="Common C routines used by Gtk+ and other libs"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL')
options=('!libtool')
install=glib.install
source=(ftp://ftp.gtk.org/pub/gtk/v1.2/${pkgname}-${pkgver}.tar.gz \
        patch-ab
        patch-ac
        patch-ae
        patch-af
        patch-glib.m4
        patch-glib.pc.in
        patch-gmodule.pc.in
        patch-gmodule__gmodule-dl.c
        patch-gstrfuncs.c
        patch-gthread.pc.in
        patch-gutils.c
        )
md5sums=('6fe30dad87c77b91b632def29dd69ef9'
         '0241ba6343f18dd531f0de2148d0c27e'
         '6f59d1b7ee9e891516fc0fdee9118c4e'
         '8908fb1d358605b0cf48fe30fcbea988'
         '7a005beca74f2febe586baf3021ef5bb'
         '741f0dee70d69b4df5e17b335021ef51'
         '5d84006e9e577e65852cdb11b2dd78ff'
         '4b08f8a4855de72f7a037a5afd2a4885'
         '21d36e69b7ef84ab1ba4e15465cf0bda'
         'ee4a3df330ed981dbd0fecd48582c53a'
         'e17d31e7c167d20ea91194c93b31317a'
         'eb0da0899991aec45bede4b756529076')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  for i in patch-ab patch-ac patch-ae patch-af patch-glib.m4 patch-glib.pc.in patch-gmodule.pc.in patch-gmodule__gmodule-dl.c patch-gstrfuncs.c patch-gthread.pc.in patch-gutils.c
  do
  	patch -p0 -i "${srcdir}/$i"
  done

  find . -name "configure" | xargs sed -i '' -e \
  	's|-lpthread|-pthread|g ; s|-lc_r|-pthread|g'

  export CFLAGS="${CFLAGS} -std=gnu89"
  ./configure --prefix=/usr/local --mandir=/usr/local/man \
              --infodir=/usr/local/info \
              --includedir=/usr/local/include/glib12 \
              --host=$CHOST --target=$CHOST
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  ln -sf "${pkgdir}/usr/local/bin/glib-config" "${pkgdir}/usr/local/bin/glib12-config"
}
