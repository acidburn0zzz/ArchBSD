# $Id: PKGBUILD 162914 2012-07-03 20:59:56Z eric $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

pkgname=libjpeg-turbo
pkgver=1.2.1
pkgrel=1
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression"
arch=('i686' 'x86_64')
url="http://libjpeg-turbo.virtualgl.org/"
license=('GPL' 'custom')
makedepends=('nasm')
provides=('libjpeg=8.0.2')
conflicts=('libjpeg')
replaces=('libjpeg')
source=(http://sourceforge.net/projects/$pkgname/files/$pkgver/$pkgname-$pkgver.tar.gz)
sha1sums=('a4992e102c6d88146709e8e6ce5896d5d0b5a361')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # put documentation in a reasonable directory...
  sed -i '' -e "/docdir = /s#/doc#/doc/libjpeg-turbo#" Makefile.in
  sed -i '' -e "/exampledir = /s#/doc#/doc/libjpeg-turbo/examples#" Makefile.in

  ./configure --prefix=/usr/local --with-jpeg8 --mandir=/usr/local/man --with-pic
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  MKDIRPROG="mkdir -p" make DESTDIR="$pkgdir/" install

  # provide jpegint.h as it is required by various software
  install -m644 jpegint.h "$pkgdir/usr/local/include/"

  # do not distributre libturbojpeg as it is unversioned
  rm "$pkgdir"/usr/local/lib/libturbojpeg.*
  rm "$pkgdir/usr/local/include/turbojpeg.h"
  rm "$pkgdir/usr/local/bin/tjbench"

  install -dm755 "$pkgdir/usr/local/share/licenses/libjpeg-turbo/"
  ln -s ../../doc/libjpeg-turbo/README \
    "$pkgdir/usr/local/share/licenses/libjpeg-turbo/README"
  ln -s ../../doc/libjpeg-turbo/README-turbo.txt \
    "$pkgdir/usr/local/share/licenses/libjpeg-turbo/README-turbo.txt"

  ln -s libjpeg.so.8.2 "${pkgdir}/usr/local/lib/libjpeg.so"
}
