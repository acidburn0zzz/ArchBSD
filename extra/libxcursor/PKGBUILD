# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=libxcursor
pkgver=1.1.13
pkgrel=1
pkgdesc="X cursor management library"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxfixes' 'libxrender')
makedepends=('xorg-util-macros')
source=(${url}/releases/individual/lib/libXcursor-${pkgver}.tar.bz2)
sha1sums=('4e461d9e4324689ca8a84706926a8c0fe9470e47')

build() {
  cd "${srcdir}/libXcursor-${pkgver}"
  ./configure --prefix=/usr/local --sysconfdir=/usr/local/etc \
              --with-icondir=/usr/local/lib/X11/icons
  make
}

package() {
  cd "${srcdir}/libXcursor-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/local/libdata"
  mv "${pkgdir}/usr/local/lib/pkgconfig" \
     "${pkgdir}/usr/local/libdata/pkgconfig"

  install -m755 -d "${pkgdir}/usr/local/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/local/share/licenses/${pkgname}/"
}
