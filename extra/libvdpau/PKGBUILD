# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=libvdpau
pkgver=0.6
pkgrel=1
pkgdesc="Nvidia VDPAU library"
arch=(i686 x86_64)
url='http://cgit.freedesktop.org/~aplattner/libvdpau'
conflicts=('nvidia-utils<190.42-2')
license=('custom')
source=(http://people.freedesktop.org/~aplattner/vdpau/${pkgname}-${pkgver}.tar.gz
        libvdpau.install)
makedepends=('libx11')
sha1sums=('255c9bddbfa5575ab08e91dd1e315a693256a7dc'
          'f99b42ac4fb5318724382c2d8956a5cc7699a89f')
backup=('/usr/local/etc/vdpau_wrapper.cfg')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '' -e 's|\$(libdir)/\(pkgconfig\)|$(prefix)/libdata/\1|' \
    Makefile.am
  sed -i '' -e '/^export /d' doc/Makefile.am

  mv src/vdpau_wrapper.cfg src/vdpau_wrapper.cfg.sample
# ports does this on Makefile.am instead of .in and reruns autotools
# but this is nicer - autotools as makedepnds is nasty
  sed -i '' -e 's/vdpau_wrapper.cfg/&.sample/' src/Makefile.in

  ./configure --prefix=/usr/local --sysconfdir=/usr/local/etc --disable-documentation
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/local/libdata"
  mv "${pkgdir}/usr/local/lib/pkgconfig" \
     "${pkgdir}/usr/local/libdata/pkgconfig"

  install -d -m755 "${pkgdir}/usr/local/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/local/share/licenses/${pkgname}/"
}
