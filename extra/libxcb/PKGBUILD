pkgname=libxcb
pkgver=1.9
pkgrel=1
pkgdesc="X11 client-side library"
arch=(i686 x86_64)
url="http://xcb.freedesktop.org/"
depends=('xcb-proto>=1.7' 'libxdmcp' 'libxau')
makedepends=('pkgconfig' 'libxslt' 'python2')
options=('!libtool')
license=('custom')
source=($url/dist/$pkgname-$pkgver.tar.bz2
        libxcb-1.1-no-pthread-stubs.patch
	patch-tests-check_public.c)
sha1sums=('ad2fb95eeec41ba3d39502a4f7460c3b64fdf061'
          '05d6177486b211b560364813b713777ab894347c'
          'fe8f00016e6cdb4dff52c1eaba9288e300c85deb')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/libxcb-1.1-no-pthread-stubs.patch"
  patch -p0 -i "$srcdir/patch-tests-check_public.c"

  PYTHON=/usr/local/bin/python2
  ./autogen.sh --prefix=/usr/local \
    --enable-xinput \
    --enable-xkb \
    --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname"
}
