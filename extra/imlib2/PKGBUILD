# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Arjan Timmerman <arjan.archlinux.org>
# Contributor: Tom Newsom <Jeepster.gmx.co.uk>

pkgname=imlib2
pkgver=1.4.5
pkgrel=2
pkgdesc="Library that does image file loading and saving as well as rendering, manipulation, arbitrary polygon support"
url="http://sourceforge.net/projects/enlightenment/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('libtiff' 'giflib' 'freetype2' 'libxext' 'libpng' 'libid3tag' 'libjpeg-turbo')
options=('!libtool')
source=("http://downloads.sourceforge.net/enlightenment/$pkgname-$pkgver.tar.bz2")
sha1sums=('af86a2c38f4bc3806db57e64e74dc9814ad474a0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # disable optimizations, they cause problems (e.g. FS#12268)
  [ $CARCH = "i686" ] && EXTRAOPTS="--disable-mmx"
  [ $CARCH = "x86_64" ] && EXTRAOPTS="--disable-amd64"

  # Configure and Build
  ./configure --prefix=/usr/local/ \
              --sysconfdir=/usrlocal/etc/imlib2 \
              --x-libraries=/usr/local/lib $EXTRAOPTS
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # Install License
  install -dm755 "$pkgdir/usr/local/share/licenses/$pkgname/"
  install -m644 COPYING "$pkgdir/usr/local/share/licenses/$pkgname/COPYING"
}

