# Contributor: Jan de Groot <jgc@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=xorg-font-util
pkgver=1.3.0
pkgrel=1
pkgdesc='X.Org font utilities'
url='http://xorg.freedesktop.org/'
arch=('i686' 'x86_64')
license=('custom')
makedepends=('xorg-util-macros')
source=("${url}/releases/individual/font/font-util-${pkgver}.tar.bz2")
sha1sums=('a015f4c5a35d98af72835797293697e0eb9cd506')

replaces=('font-util')
provides=('font-util')
conflicts=('font-util')
groups=('xorg-fonts' 'xorg')

build() {
	cd "${srcdir}/font-util-${pkgver}"

	./configure \
		--prefix=/usr/local \
		--mandir=/usr/local/man \
		--with-mapdir=/usr/local/lib/X11/fonts/util

	make
}

package() {
	cd "${srcdir}/font-util-${pkgver}"
	make DESTDIR="${pkgdir}" install

    install -dm755 "${pkgdir}/usr/local/libdata"
    mv "${pkgdir}/usr/local/lib/pkgconfig" \
       "${pkgdir}/usr/local/libdata/pkgconfig"

	install -dm755 "${pkgdir}/usr/local/share/licenses/${pkgname}/"
	install -m644 COPYING "${pkgdir}/usr/local/share/licenses/${pkgname}/COPYING"
}
