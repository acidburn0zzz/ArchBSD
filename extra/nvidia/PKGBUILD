# Contributor: Wolfgang Bumiller <blub at speed dot at>

pkgname=nvidia
pkgver=313.18
pkgrel=1
pkgdesc="NVidia graphics card binary drivers"
arch=('i686' 'x86_64')
license=('NVIDIA')
makedepends=('freebsd-source')
url="http://www.nvidia.com/"
install=nvidia.install
source=("ftp://download.nvidia.com/XFree86/FreeBSD-${CARCH}/${pkgver}/NVIDIA-FreeBSD-${CARCH}-${pkgver}.tar.gz")
md5sums=('d698f4dcbcfd18fd447946e50e976c71')

build() {
  cd "${srcdir}/NVIDIA-FreeBSD-${CARCH}-${pkgver}/"
  unset CFLAGS LDFLAGS CPPFLAGS CXXFLAGS
  make
}

package() {
  cd "${srcdir}/NVIDIA-FreeBSD-${CARCH}-${pkgver}/"

# Kernel module
  install -dm755 "${pkgdir}/usr/local/modules"
  install -m555 src/nvidia.ko "${pkgdir}/usr/local/modules/nvidia.ko"

# OpenGL Libraries
  install -dm755 "${pkgdir}/usr/local/lib"
  for i in libGL.so.1 libglx.so.1 \
  	       libnvidia-cfg.so.1 \
  	       libnvidia-glcore.so.1 \
  	       libnvidia-tls.so.1 \
           libvdpau.so.1 \
           libvdpau_trace.so.1 \
           libvdpau_nvidia.so.1
  do
    install -m755 "obj/$i" "${pkgdir}/usr/local/lib/$i"
    ln -sv "$i" "${pkgdir}/usr/local/lib/${i%%.1}"
  done

# X11 driver
  install -dm755 "${pkgdir}/usr/local/lib/xorg/modules/drivers"
  install -m755 "obj/nvidia_drv.so" "${pkgdir}/usr/local/lib/xorg/modules/drivers/nvidia_drv.so"

# tools
  install -dm755 "${pkgdir}/usr/local/bin"
  install -m755 "obj/nvidia-bug-report.sh" "${pkgdir}/usr/local/bin/"
  install -m755 "obj/nvidia-settings"      "${pkgdir}/usr/local/bin/"
  install -m755 "obj/nvidia-xconfig"       "${pkgdir}/usr/local/bin/"
}
