pkgname=openrc
pkgver=0.11.8
pkgrel=2
pkgdesc='Dependency based init system that works with sysvinit.'
arch=('i686' 'x86_64')
groups=('base')
url='http://www.gentoo.org/proj/en/base/openrc/'
license=('BSD')

backup=(etc/inittab
        etc/rc.conf
        etc/conf.d/{bootmisc,consolefont,dmesg,fsck,hostname,hwclock,keymaps}
        etc/conf.d/{killprocs,localmount,modules,netmount,network,staticroute}
        etc/conf.d/{tmpfiles,urandom})
source=(http://dev.gentoo.org/~williamh/dist/$pkgname-$pkgver.tar.bz2
	'openrc.patch'
	'conf.d-sshd'
	'init.d-sshd'
)
md5sums=('d896371d533a22de4cf57c7371c4d6e8'
         '2ea45acde0757636ca4f0265121174d6'
         'b35e9f3829f4cfca07168fcba98749c7'
         '29f5aaefd9fc245a61535a19bab75b30')


_makeargs=(BRANDING='Arch BSD')

build() {
    cd "$srcdir"/$pkgname-$pkgver
    #For some reason it fails to build the binaries for FreeBSD:
    #Thsi fixes the 'exec format error' on boot
    patch -p1 < "${srcdir}/openrc.patch"
    sed -i '' -e 's|os-${OS}|os-FreeBSD|' mk/os.mk
    gmake "${_makeargs}"
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    gmake DESTDIR="$pkgdir" "${_makeargs[@]}" install
    rm -rf ${pkgdir}/usr/share/man/man8/rc.8

    install -dm755 "${pkgdir}/etc/init.d"
    install -dm755 "${pkgdir}/etc/conf.d"
    install -m755 "${srcdir}/init.d-sshd" "${pkgdir}/etc/init.d/sshd"
    install -m644 "${srcdir}/conf.d-sshd" "${pkgdir}/etc/conf.d/sshd"
}

