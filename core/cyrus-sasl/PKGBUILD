# $Id: PKGBUILD 168631 2012-10-13 16:37:23Z thomas $
# Maintainer: Jan de Groot <jgc@archlinux.org>

# This package spans multiple repositories. 
# Always build from cyrus-sasl/trunk and merge changes to libsasl/trunk.

pkgbase=('cyrus-sasl')
pkgname=('cyrus-sasl' 'cyrus-sasl-gssapi' 'cyrus-sasl-ldap' 'cyrus-sasl-sql')
#pkgname=libsasl
pkgver=2.1.26
pkgrel=2
pkgdesc="Cyrus Simple Authentication Service Layer (SASL) library"
arch=('i686' 'x86_64')
url="http://cyrusimap.web.cmu.edu/"
license=('custom')
options=('!makeflags')
makedepends=('libmysqlclient' 'libldap')
# FIXME: conf.d/saslauthd init.d/saslauthd
source=(ftp://ftp.cyrusimap.org/cyrus-sasl/cyrus-sasl-${pkgver}.tar.gz
        patch-Makefile.in
        patch-config__ltmain.sh
        patch-configure
        patch-include__sasl.h
        patch-plugins__ldapdb.c
        patch-saslauthd__Makefile.in
        patch-saslauthd__configure
        patch-saslauthd__saslcache.c
)

build() {
  cd "${srcdir}/cyrus-sasl-${pkgver}"

  for i in patch-Makefile.in patch-config__ltmain.sh patch-configure patch-include__sasl.h patch-plugins__ldapdb.c patch-saslauthd__Makefile.in patch-saslauthd__configure patch-saslauthd__saslcache.c
  do
    patch -p0 -i "$srcdir/$i"
  done

# Arch does this
# rm -f config/config.guess config/config.sub
# rm -f config/ltconfig config/ltmain.sh config/libtool.m4
# rm -fr autom4te.cache
# libtoolize -c
# aclocal -I config -I cmulocal
# automake -a -c
# autoheader
# autoconf
#
# pushd saslauthd
# rm -f config/config.guess config/config.sub
# rm -f config/ltconfig config/ltmain.sh config/libtool.m4
# rm -fr autom4te.cache
# libtoolize -c
# aclocal -I config -I ../cmulocal -I ../config
# automake -a -c
# autoheader
# autoconf
# popd

  export CFLAGS="${CFLAGS} -fPIC"
  export LDFLAGS="${LDFLAGS} -fPIC"
  ./configure --prefix=/usr/local \
      --mandir=/usr/local/man \
      --infodir=/usr/local/info \
      --sysconfdir=/usr/local/etc \
      --with-configdir=/usr/local/etc/sasl2:/usr/local/lib/sasl2 \
      --with-plugindir=/usr/local/etc/sasl2 \
      --with-dbpath=/usr/local/sasldb2 \
      --with-lib-subdir=lib \
      --with-pkgconfigdir=/usr/local/libdata/pkgconfig \
      --includedir=/usr/local/include \
      --enable-static \
      --enable-auth-sasldb \
      --with-rc4=openssl \
      --with-openssl=yes \
      --enable-ldapdb \
      --with-ldap=/usr/local \
      --with-saslauthd=/var/run/saslauthd \
      --enable-gssapi=/usr --with-gss_impl=mit \
      --disable-krb4 \
      --enable-alwaystrue \
      --enable-checkapop \
      --enable-cram \
      --enable-digest \
      --disable-otp \
      --disable-srp \
      --disable-srp-setpass \
      --enable-plain \
      --enable-anon \
      --enable-login \
      --enable-ntlm \
      --disable-passdss \
      --enable-sql \
      --disable-macos-framework \
      --with-pam \
      --with-devrandom=/dev/urandom
  make
}

package_libsasl() {
  pkgdesc="Cyrus Simple Authentication Service Layer (SASL) Library"
  conflicts=('cyrus-sasl-plugins')

  cd "${srcdir}/cyrus-sasl-${pkgver}"
  for dir in include lib sasldb plugins utils; do
    pushd ${dir}
    make DESTDIR="${pkgdir}" install
    popd
  done
  rm -f "${pkgdir}"/usr/local/lib/sasl2/libsql.so*
  rm -f "${pkgdir}"/usr/local/lib/sasl2/libgssapiv2.so*
  rm -f "${pkgdir}"/usr/local/lib/sasl2/libldapdb.so*
  install -m755 -d "${pkgdir}/usr/local/share/licenses/libsasl"
  install -m644 COPYING "${pkgdir}/usr/local/share/licenses/libsasl/"
}

package_cyrus-sasl() {
  depends=("libsasl=${pkgver}")
  pkgdesc="Cyrus saslauthd SASL authentication daemon"
  backup=('usr/local/etc/conf.d/saslauthd')

  cd "${srcdir}/cyrus-sasl-${pkgver}/saslauthd"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/local/share/licenses/cyrus-sasl"
  ln -sf ../libsasl/COPYING "${pkgdir}/usr/local/share/licenses/cyrus-sasl/"
}

package_cyrus-sasl-gssapi() {
  pkgdesc="GSSAPI authentication mechanism for Cyrus SASL"
  depends=("libsasl=${pkgver}" 'krb5')
  replaces=('cyrus-sasl-plugins')

  cd "${srcdir}/cyrus-sasl-${pkgver}/plugins"
  install -m755 -d "${pkgdir}/usr/local/lib/sasl2"
  cp -a .libs/libgssapiv2.so* "${pkgdir}/usr/local/lib/sasl2/"

  install -m755 -d "${pkgdir}/usr/local/share/licenses/cyrus-sasl-gssapi"
  ln -sf ../libsasl/COPYING "${pkgdir}/usr/local/share/licenses/cyrus-sasl-gssapi/"
}

package_cyrus-sasl-ldap() {
  pkgdesc="ldapdb auxprop module for Cyrus SASL"
  depends=("libsasl=${pkgver}" 'libldap')
  replaces=('cyrus-sasl-plugins')

  cd "${srcdir}/cyrus-sasl-${pkgver}/plugins"
  install -m755 -d "${pkgdir}/usr/local/lib/sasl2"
  cp -a .libs/libldapdb.so* "${pkgdir}/usr/local/lib/sasl2/"

  install -m755 -d "${pkgdir}/usr/local/share/licenses/cyrus-sasl-ldap"
  ln -sf ../libsasl/COPYING "${pkgdir}/usr/local/share/licenses/cyrus-sasl-ldap/"
}

package_cyrus-sasl-sql() {
  pkgdesc="SQL auxprop module for Cyrus SASL"
  depends=("libsasl=${pkgver}" 'libmysqlclient')
  replaces=('cyrus-sasl-plugins')

  cd "${srcdir}/cyrus-sasl-${pkgver}/plugins"
  install -m755 -d "${pkgdir}/usr/local/lib/sasl2"
  cp -a .libs/libsql.so* "${pkgdir}/usr/local/lib/sasl2/"

  install -m755 -d "${pkgdir}/usr/local/share/licenses/cyrus-sasl-sql"
  ln -sf ../libsasl/COPYING "${pkgdir}/usr/local/share/licenses/cyrus-sasl-sql/"
}
