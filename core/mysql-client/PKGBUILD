pkgbase=mysql
pkgname=('libmysqlclient' 'mysql-clients' 'mysql')
pkgver=5.5.29
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
url="https://www.mysql.com/products/community/"
makedepends=('cmake')
source=("http://ftp.gwdg.de/pub/misc/mysql/Downloads/MySQL-5.5/${pkgbase}-${pkgver}.tar.gz"
        'my.cnf' 'mysqld-post.sh' 'mysqld-tmpfile.conf'
        'patch-cmake_plugin.cmake'
        'patch-extra_yassl_taocrypt_src_integer.cpp'
        'patch-include_CMakeLists.txt'
        'patch-include_my_compare.h'
        'patch-include_myisam.h'
        'patch-mysys_default.c'
        'patch-scripts_mysqld_safe.sh'
        'patch-sql_sys_vars.cc'
)
md5sums=('e6b9f9cb82e990bd8f0474df7462904e'
         '2716c6ec912d791abb369579a95ab527'
         '35d71489f58ae6c02f5d3281b2578c79'
         '2fa6e456964d4ff5e6d4f9ff0126aed6'
         '52d27b3a1333dd4c63efff3a1431061f'
         '728e0a7521b330c3696ee8aac43dbc2e'
         '95617bf2c55e270b879a02f74cd6d03a'
         '5587191c167ebd33b3ed9946032cd2f1'
         'ccb4b565784d6d4f0493d56c16902c69'
         'fcb52c109a11e9252ef6bd5c24018865'
         '3b97497dc7e075bcd72460d24e2001a1'
         '5aa461540f58e699c13ce03cbf3a9f3c')

build() {

  # CFLAGS/CXXFLAGS as suggested upstream

  cd "${srcdir}"/${pkgbase}-${pkgver}

  for i in patch-cmake_plugin.cmake patch-extra_yassl_taocrypt_src_integer.cpp patch-include_CMakeLists.txt patch-include_my_compare.h patch-include_myisam.h patch-mysys_default.c patch-scripts_mysqld_safe.sh patch-sql_sys_vars.cc
  do
    patch -p0 -i "${srcdir}/$i"
  done

  sed -i '' -e '/ADD_SUBDIRECTORY(tests)/d' \
            -e '/ADD_SUBDIRECTORY(mysql-test.*)/d' \
            CMakeLists.txt

  mkdir ${srcdir}/build
  cd ${srcdir}/build

  cmake ../${pkgbase}-${pkgver} \
        -DINSTALL_DOCDIR="share/doc/mysql" \
        -DINSTALL_DOCREADMEDIR="share/doc/mysql" \
        -DINSTALL_INCLUDEDIR="include/mysql" \
        -DINSTALL_INFODIR="info" \
        -DINSTALL_LIBDIR="lib/mysql" \
        -DINSTALL_MANDIR="man" \
        -DINSTALL_MYSQLDATADIR="/var/db/mysql" \
        -DINSTALL_MYSQLSHAREDIR="share/mysql" \
        -DINSTALL_MYSQLTESTDIR="share/mysql/tests" \
        -DINSTALL_PLUGINDIR="lib/mysql/plugin" \
        -DINSTALL_SBINDIR="libexec" \
        -DINSTALL_SCRIPTDIR="bin" \
        -DINSTALL_SHAREDIR="share" \
        -DINSTALL_SQLBENCHDIR="share/mysql" \
        -DINSTALL_SUPPORTFILESDIR="share/mysql" \
        -DWITH_LIBEDIT=0 \
        -DWITH_LIBWRAP=1 \
        -DWITH_SSL=bundled \
        -DWITH_EMBEDDED_SERVER="ON" \
        -DWITH_MYSQLD_LDFLAGS="${LDFLAGS}" \
        -DCMAKE_C_FLAGS="-fPIC ${CFLAGS} -fno-strict-aliasing -DBIG_JOINS=1" \
        -DCMAKE_CXX_FLAGS="-fPIC ${CXXFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -felide-constructors -fno-rtti" \
        -DCMAKE_EXE_LINKER_FLAGS="${LDFLAGS}" \
        -DCMAKE_MODULE_LINKER_FLAGS="${LDFLAGS}" \
        -DCMAKE_SHARED_LINKER_FLAGS="${LDFLAGS}" \
        -DCMAKE_INSTALL_PREFIX:PATH=/usr/local \
        -DCMAKE_BUILD_TYPE:STRING=Release \
        -DTHREADS_HAVE_PTHREAD_ARG:BOOL=YES \
        -DCMAKE_VERBOSE_MAKEFILE:BOOL=ON \
        -DWITH_INNOBASE_STORAGE_ENGINE=1 \
        -DWITH_PARTITION_STORAGE_ENGINE=1 \
        -DWITHOUT_EXAMPLE_STORAGE_ENGINE=1 \
        -DWITHOUT_ARCHIVE_STORAGE_ENGINE=1 \
        -DWITHOUT_BLACKHOLE_STORAGE_ENGINE=1 \
        -DWITHOUT_FEDERATED_STORAGE_ENGINE=1 \

 cd ${srcdir}/build
# it fails to create a dir inside this one otherwise
  mkdir -p sql/share
  make
}

package_libmysqlclient(){
  pkgdesc="MySQL client libraries"
  depends=('openssl')
  cd ${srcdir}/build

  for dir in include libmysql libmysqld libservices; do
    make -C ${dir} DESTDIR="${pkgdir}" install
  done

  install -d "${pkgdir}"/usr/local/bin
  install -m755 scripts/mysql_config "${pkgdir}"/usr/local/bin/
  install -d "${pkgdir}"/usr/local/man/man1

  for man in mysql_config mysql_client_test_embedded mysqltest_embedded; do
    install -m644 "${srcdir}"/${pkgbase}-${pkgver}/man/$man.1 "${pkgdir}"/usr/local/man/man1/$man.1
  done

}

package_mysql-clients(){
  pkgdesc="MySQL client tools"
  depends=('libmysqlclient')
  cd ${srcdir}/build

  make -C client DESTDIR="${pkgdir}" install

  # install man pages
  install -d "${pkgdir}"/usr/local/man/man1

  for man in mysql mysqladmin mysqlcheck mysqldump mysqlimport mysqlshow mysqlslap; do
    install -m644 "${srcdir}"/${pkgbase}-${pkgver}/man/$man.1 "${pkgdir}"/usr/local/man/man1/$man.1
  done

  # provided by mysql
  rm "${pkgdir}"/usr/local/bin/{mysql_{plugin,upgrade},mysqlbinlog,mysqltest}
}

package_mysql(){
  pkgdesc="A fast SQL database server"
  backup=('etc/mysql/my.cnf')
  install=mysql.install
  depends=('mysql-clients')
  options=('emptydirs')
  cd ${srcdir}/build

  make DESTDIR="${pkgdir}" install
  install -dm644 "${pkgdir}"/usr/local/etc/mysql/
  install -dm644 "${pkgdir}"/usr/local/bin/mysqld-post
  install -m755 "${srcdir}"/my.cnf "${pkgdir}"/usr/local/etc/mysql/
  install -m755 "${srcdir}"/mysqld-post.sh "${pkgdir}"/usr/local/bin/mysqld-post
  install -m644 "${srcdir}"/mysqld-tmpfile.conf "${pkgdir}"/usr/local/lib/tmpfiles.d/mysqld.conf

  # provided by libmysqlclient
  rm "${pkgdir}"/usr/local/bin/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}
  rm "${pkgdir}"/usr/local/lib/libmysql*
  rm -r "${pkgdir}"/usr/local/include/
  rm "${pkgdir}"/usr/local/man/man1/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}.1
  # provided by mysql-clients
  rm "${pkgdir}"/usr/local/bin/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}
  rm "${pkgdir}"/usr/local/man/man1/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}.1
  rm -r "${pkgdir}"/usr/local/{data,mysql-test,sql-bench}
  rm "${pkgdir}"/usr/local/man/man1/mysql-test-run.pl.1
  install -dm700 "${pkgdir}"/var/lib/mysql
}
